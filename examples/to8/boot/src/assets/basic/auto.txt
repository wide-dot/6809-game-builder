
10 ' *****************************
20 ' ** AFFICHAGE FICHIERS MAP  **
30 ' ** Auteur: Samuel DEVULDER **
40 ' ** Date:   5 sept 2009     **
50 ' *****************************
60 PALETTE 0,0:PALETTE 7,4095:LOCATE 0,0,0:COLOR 7,0:SCREEN,,0:CLS
70 CLEAR 10000:DEFINT A-Z
80 DIM N$(90)
90 ' LECTURE DES FICHIERS
100 ON ERROR GOTO 480
110 FOR D=0 TO 3
120   FOR S=3 TO 16
130     A$=DSKI$(D,20,S)
140     IF A$="" THEN 200
150     N$=LEFT$(A$,8):P$=MID$(A$,9,3):A$=MID$(A$,33)
160     IF A$<>"" AND ASC(LEFT$(N$,1))=0 THEN 150
170     IF P$="MAP" THEN N$(NC)=CHR$(48+D)+":"+N$:NC=NC+1:PRINTN$;"  ";
180   IF LEFT$(N$,1)=CHR$(255) THEN S=16: GOTO 200
190     IF A$<>"" THEN 150
200   NEXT
210 NEXT
220 ' LECTURE ET AFFICHAGE
230 SIZE=20000
240 DIM T(SIZE)
250 DEF FNC(R)=MAX(-R-1,R)
260 DARK=0:FIRST=1
270 DO
280   FOR J=0 TO NC-1
290     LOADP N$(J),T(SIZE)
300     ' ATTENTE
310     C=0: IF FIRST=1 THEN FIRST=0: GOTO 330
320     IF INKEY$="" AND PTRIG=0 AND C<1000 THEN C=C+1:GOTO 320
330     ' EFFACEMENT
340     COLOR DARK,DARK:CLS:COLOR 15
350     T=T(SIZE)
360     T=T+1: IF T(T)<>-23206 THEN GOTO 450
370     FOR I=15 TO 0 STEP -1: T=T+1:PALETTE I, FNC(T(T)) AND 4095:NEXT
380     T=T+1: CONSOLE,,,,T(T)
390     T=T+1: DARK=T(T): COLOR DARK,DARK: SCREEN,,DARK:CLS
400     T=T+1: POKE &H605F,T(T)
410     PUT(0,0),T(SIZE)
420   NEXT
430 LOOP
440 END
450 RESTORE 460: FOR I=0 TO 15: READ P: PALETTE I,FNC(P):NEXT
460 DATA -1,-16,-241,-256,-3841,-3856,-4081,-4096,-1912,-827,-932,-939,-2612,-2619,-3816,-124
470 GOTO 410
480 E=ERR:L=ERL:IF E=71 OR E=53 THEN S=16:RESUME 200
490 PRINT"ERROR ";E;" LINE ";L